fn create_wave_window($x, $y, $xsize, $ysize)
{
    $this = new()
    
    $this.data = swarm.wav_data
    $this.x = $x
    $this.y = $y
    $this.xsize = $xsize
    $this.ysize = $ysize

    $this.plotmax = new($xsize)
    $this.plotmin = new($xsize)
    
    $this.posx = $x
    $this.posy = $y
                
    $this.touch = 0
    
    $i = 0
    $step = get_size($this.data) / $xsize
    while ($i < $xsize)
    {
        $max = $this.data[$i * $step]
        $min = $this.data[$i * $step]
        
        $j = 1
        while ($j < $step)
        {
            if $this.data[$i * $step + $j] > $max
                {$max = $this.data[$i * $step + $j]}
            
            if $this.data[$i * $step + $j] < $min
                {$min = $this.data[$i * $step + $j]}
                
            $j + 1
        }
        
        $this.plotmax[$i] = scale($max, -(1<<15), (1<<15), -$this.ysize/2, $this.ysize/2)       
        $this.plotmin[$i] = scale($min, -(1<<15), (1<<15), -$this.ysize/2, $this.ysize/2)
        
        $i + 1
    }
    
    
 
    $this.update =
    {
        $this = $1
        $touch_state = $2
        
        
        if $touch_state.down
        {
            $this.touch = 1
        }
        if $touch_state.up
        {
            $this.touch = 0
        }
        
        if $this.touch && 
             $touch_state.x > $this.x && $touch_state.x < $this.x + $this.xsize &&
             $touch_state.y > $this.y && $touch_state.y < $this.y + $this.ysize
        {
            $this.posx = $touch_state.x
            swarm.window_start = scale($this.posx, $this.x, $this.x + $this.xsize, 0, swarm.wav_size)
            
            
            
            $this.posy = $touch_state.y 
            swarm.window_length = scale($this.posy, $this.y + $this.ysize, $this.y, 100, swarm.wav_size)
            
            if swarm.window_length + swarm.window_start >= swarm.wav_size
            {
                swarm.window_length = swarm.wav_size - swarm.window_start
            }     
          
            
            
            ret(1)     
        }
        
        ret(0)
    }
        
    
    
    
    $this.draw = 
    {
        $this = $1
    
        fbox($this.x, $this.y, $this.xsize, $this.ysize, get_blend(BLACK, WHITE, 20))
        box($this.x - 1, $this.y - 1, $this.xsize + 2, $this.ysize + 2, WHITE)
        $midline = $this.y + $this.ysize / 2
        
        $i = 0
        while ($i < $this.xsize)
        {
            line($i + $this.x, $midline + $this.plotmax[$i], $i + $this.x, $midline + $this.plotmin[$i], GREEN)
            $i + 1
        }
        
       
                
        line($this.x, $midline, $this.x + $this.xsize, $midline, WHITE)
        
        //xy lines
        line($this.posx, $this.y, $this.posx, $this.y + $this.ysize, BLUE)
        //line($this.x, $this.posy, $this.x + $this.xsize, $this.posy, RED)
        $end = scale(swarm.window_start + swarm.window_length, 0, swarm.wav_size, $this.x, $this.x + $this.xsize)
        line($end, $this.y, $end, $this.y + $this.ysize, BLUE)
        
        transp(20)
        fbox($this.posx, $this.y, $end - $this.posx, $this.ysize, BLUE)
        transp(255)
        
        $p = 0
        while ($p < swarm.voice_count)
        {
            if swarm.pointers[$p] >= swarm.window_start &&
                swarm.pointers[$p] < swarm.window_start + swarm.window_length
            {
                 $c = get_blend(YELLOW, RED, scale(swarm.volumes[$p], 0, 1, 0, 255))
            }
            else
            {
                 $c = get_blend(WHITE, BLACK, 150)
            }
            
            fbox(scale(swarm.pointers[$p], 0, swarm.wav_size, $this.x, $this.x + $this.xsize), 
                scale($p, 0, swarm.voice_count, $this.y + 1, $this.y + $this.ysize - 1),
                5, 5, $c)
            
            $p + 1
        }
    }   
    
    
    
    
    
    ret ($this)
}
