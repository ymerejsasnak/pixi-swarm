// function to create a button container
// $x, $y are for upper left position
// $xsize, $ysize are button's size of course
// $text is the string that appears in the button
// $func is the function to call when this button is pressed
//      (can either be a function name (ie pointer) or a series of statements in {} )

fn create_button($x, $y, $xsize, $ysize, $text, $func)
{

	$this = new()
	
	$this.text = $text
	$this.x = $x
	$this.y = $y
	$this.xsize = $xsize
	$this.ysize = $ysize
	
	$this.func = $func

    // state of button
  	$this.on = 0

		
	// button draw function
	$this.draw =
	{
	    $this = $1
	    
	    if $this.on
	    {
		    fbox($this.x, $this.y, $this.xsize, $this.ysize, get_blend(BLACK, WHITE, 210))
		    box($this.x, $this.y, $this.xsize, $this.ysize, get_blend(BLACK, WHITE, 40))
		    print($this.text, $this.x + $this.xsize / 2, 
		          $this.y + $this.ysize / 2, BLACK)
		}
		else
		{
		    fbox($this.x, $this.y, $this.xsize, $this.ysize, get_blend(BLACK, WHITE, 40))
		    box($this.x, $this.y, $this.xsize, $this.ysize, get_blend(BLACK, WHITE, 210))
		    print($this.text, $this.x + $this.xsize / 2,
		          $this.y + $this.ysize / 2, WHITE)		
		}
	}	
	
	
	// update button based on touch state
	$this.update = 
    {
        $this = $1
        $touch_state = $2
       
        // reset if already turned on
        if $this.on
        {
            $this.on = 0
        }
                     
        // trigger func when clicked and reset touch_state.down
        if $touch_state.down && $this.on == 0
        {
            if $touch_state.x > $this.x && $touch_state.x < $this.x + $this.xsize &&
               $touch_state.y > $this.y && $touch_state.y < $this.y + $this.ysize
            {
                $this.on = 1
                $this.func()
                $touch_state.down = 0
            }
        }
	}   
    
	ret ($this)
}



